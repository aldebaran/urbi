/*
 * Copyright (C) 2010, Gostai S.A.S.
 *
 * This software is provided "as is" without warranty of any kind,
 * either expressed or implied, including but not limited to the
 * implied warranties of fitness for a particular purpose.
 *
 * See the LICENSE file for more information.
 */

requireFile("urbi/loadable.u");
requireFile("urbi/nil.u");
requireFile("urbi/channel.u");
requireFile("urbi/global.u");
requireFile("urbi/object.u");
requireFile("urbi/boolean.u");
requireFile("urbi/control.u");

do (Job)
{
  // the status of the acquired mutex is push in it to avoid calling release too much.
  var '$mutexRelease' = [];
};

const var Global.Mutex = Tag.new;
do (Mutex)
{
  var type = "Mutex";
  function asMutex() { this };
  var asString = Object.&asString;

  function init()
  {
    var this.sem = Semaphore.new(1) |
    var this.onEnter = function () { this.lock } |
    var this.onLeave = function () { this.unlock } |
  };

  function lock()
  {
    if (currentRunner.'$mutexRelease' === nil)
      currentRunner.'$mutexRelease' = [] |
    var l = currentRunner.'$mutexRelease' |
    l.insertBack(false) |
    sem.acquire |
    // This will not be executed if the semaphore is not acquired, but we
    // have to save this state because the unlock would be executed in all
    // cases.
    l.removeBack() |
    l.insertBack(true) |
  };

  function unlock()
  {
    var l = currentRunner.'$mutexRelease' |
    if (l.back)
      sem.release |
    l.removeBack() |
  };

  function '!'()
  {
    NotMutex.new(this)
  };
};

var Global.NotMutex = Mutex.new;
do (NotMutex)
{
  var type = "NotMutex";
  function asNotMutex() { this };
  var asString = Object.&asString;

  function clone()
  {
    var res = Mutex.new|
    res.protos = [NotMutex]|
    res;
  };

  function init(var mtx)
  {
    var this.mtx = mtx
  };

  function lock()
  {
    mtx.unlock
  };

  function unlock()
  {
    mtx.lock
  };
};
