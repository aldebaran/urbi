// k1 compatibility.
class Global.Kernel1
{
  setProtos([Singleton]);

  function deprecated(what, suggestion = "")
  {
    if (suggestion.empty)
      warn("`%s' is deprecated" % what)
    else
    warn("`%s' is deprecated, use `%s'" % [what, suggestion])
  };

  function copy(b)
  {
    deprecated("copy(binary)", "binary.copy") |
    b.copy
  };

  function isvoid
  {
    deprecated("isvoid(o)", "o.isVoid") |
    call.evalArgAt(0).acceptVoid.isVoid
  };

  function noop()
  {
    deprecated("noop", "{}")
  };

  function ping()
  {
    echo("pong time=%ss" % time);
  };

  function seq(n)
  {
    deprecated("seq(number)", "number.seq") |
    n.seq
  };

  function size(l)
  {
    deprecated("size(list)", "list.size") |
    l.size
  };

  function strlen(s)
  {
    deprecated("strlen(string)", "string.length") |
    s.length
  };

  function system
  {
    deprecated("system", "System") |
    System
  };

  var System.ghostID = function () { deprecated("ghostID") };

  // Deprecated commands from k1.
  // FIXME: Maybe some commands should be rewritten.
  for| (var name: [
                    "commands",
                    "connections",
                    "devices",
                    "events",
                    "functions",
                    "reset",
                    "runningcommands",
                    "strict",
                    "taglist",
                    "undefall",
                    "unstrict",
                    "uservars",
                    "vars",
                  ])
    setSlot(name,
      function () { deprecated(name) });

};

// K1 compatibility: import the k1 functions.
Global.addProto(Global.Kernel1);
