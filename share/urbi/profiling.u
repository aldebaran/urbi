/*-----------------------.
| Profiling operations.  |
`-----------------------*/

class Global.Profiling
{
  /// timen(CODE, #ITERATIONS).
  /// e.g. timen(2+2, 42).
  function timen
  {
    echo (this.new(call.argAt(0), call.argAt(0)))
  };

  /// timen(CODE, #ITERATIONS).
  /// e.g. init(2+2, 42).
  function init
  {
    if (call.argsCount != 2)
      throw ArityError.new("init", call.argsCount, 2);
    var this.expr   = call.argString(0);
    var this.niters = call.evalArgAt (1);
    var start_time = time;
    var start_cycle = cycle |

    // Bench.
    for (niters)
      call.evalArgAt (0) |

    var this.ncycles = cycle - start_cycle;
    var this.expired = time - start_time;
  };

  // Display the results.
  function asString()
  {
    if (this === Profiling)
      return "Profiling";

      "Profiling information\n"
    + "  Expression:       " + expr + "\n"
    + "  Iterations:       " + niters + "\n"
    + "  Cycles:           " + ncycles + "\n"
    + "  Total time:       " + expired + " ms\n"
    + "  Single iteration: " + expired / niters + " ms\n"
    + "                    " + ncycles / niters + " cycles";
  };
};

