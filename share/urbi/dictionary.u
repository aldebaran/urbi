do (Global.Dictionary)
{
  protos << Global.RangeIterable;

  /// Dictionary.init(KEY1, VAL1, ...).
  function init
  {
    if (call.argsCount % 2)
      throw PrimitiveError.new("init",
                               "Dictionary constructor must be given " +
			       "an even number of arguments") |
    for| (var i = 0; i < call.argsCount; i += 2)
      set(call.evalArgAt(i), call.evalArgAt(i + 1)) |
    this
  };

  function asList()
  {
    var res = []|
    for| (var k: keys)
      res.insertBack(Pair.new (k, get (k)))|
    res
  };

  function asString()
  {
    var sep = "" |
    var res = "Dictionary {" |
    for| (var k: keys)
    {
      res += sep + k.asPrintable + " => " + get(k).asPrintable |
      sep = ", " |
    }|
    res +  "}"
  };

  function getWithDefault(name, otherwise)
  {
    if (has(name))
      get(name)
    else
      otherwise
  };

  // Syntactic sugar.
  copySlot("get", "[]");
  function '[]=' (k, v)
  {
    set(k, v) |
    v
  };

};
