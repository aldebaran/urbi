/*
 * Copyright (C) 2010, 2011, Gostai S.A.S.
 *
 * This software is provided "as is" without warranty of any kind,
 * either expressed or implied, including but not limited to the
 * implied warranties of fitness for a particular purpose.
 *
 * See the LICENSE file for more information.
 */

/** Extract the Data Flow from the UObjects.
 *
 * Part of the information is gathered from the UVars, the rest by monitoring
 * the execution, using the engine hooks on UVar bind/read/write.
 *
 * This class can output a string in dot format describing the data flow
 * between UObjects. The amount of details displayed can be configured.
 */
var uobjects.DataFlow = UObject.new | do (uobjects.DataFlow)
{
  var __uobjectName = "DataFlow";
  class CustomString
  {
    var s_ = "";
    var delim_ = "<>";
    function init(s, delim="<>")
    {
      var this.s_ = s;
      var this.delim_ = delim;
    };
    function asString()
    {
      s_.asString
    };
    function asPrintable()
    {
      delim_[0] + s_.asString + delim_[1]
    };
  };
  /** Edge Filtering level.
   * 0: none
   * 1: Hide write to own uvars in init and new
   * 2: Hide write to own uvars
   */
  var filter = 1;
  /** Node filtering level
   * 0: none
   * 1: Hide unused UVars
   */
  var filterNode = 1;
  /// Show dotted lines for UVar bindings (weak information)
  var showBind = false;
  /// Make edges for UVar and method ownership (between var and owner object)
  var showOwnership = false;
  /// Show UObject statistics if available (enabled by enableStats(1)).
  var showStats = true;
  var debug = false;
  var makeShortGraph = false;
  /// Start monitoring UVar read/write/bind
  function startMonitor()
  {
    uobjects.setTrace(1);
    monitor: at(UVar.traceBind?(var obj, var uvar, var ctx))
    { // Only remember the binding object and not the function
      uvarBindings.getWithDefault(ctx[ctx.size-1][0], [], true)
        .insertUnique(uvar)|
      //if (uvar.ownerName != ctx[ctx.size-1][0])
        usedVars[uvar.asString] = 1
    };
    monitor: at(UVar.traceSet?(var obj, var uvar, var ctx))
    {
      try
      {
        uvarSetters.getWithDefault(ctx[ctx.size-1][0], Dictionary.new, true)
          .getWithDefault(ctx[ctx.size-1][1], [], true).insertUnique(uvar)|
        //if (uvar.ownerName != ctx[ctx.size-1][0])
        usedVars[uvar.asString] = 1
      }
      catch {}
    };
    monitor: at(UVar.traceGet?(var obj, var uvar, var ctx))
    {
      try
      {
        uvarGetters.getWithDefault(ctx[ctx.size-1][0], Dictionary.new, true)
          .getWithDefault(ctx[ctx.size-1][1], [], true).insertUnique(uvar) |
        //if (uvar.ownerName != ctx[ctx.size-1][0])
        usedVars[uvar.asString] = 1
      }
      catch {}
    };
  };
  /// Stop monitoring
  function stopMonitor()
  {
    uobjects.setTrace(0);
    monitor.stop;
  };
  /// Clear all data acquired by monitoring.
  function reset()
  {
    usedVars.clear();
    uvarBindings.clear();
    uvarSetters.clear();
    uvarGetters.clear();
  };
  function niceName(v)
  {
    var p = v.split(".");
    if (hasSlot(p[0]) && getSlot(p[0]).hasSlot("compactName"))
      p[0] = getSlot(p[0]).compactName
    else if  (hasSlot(p[0]) && getSlot(p[0]).hasSlot("componentName"))
      p[0] = getSlot(p[0]).componentName;
    p.join(".");
  };
  function isUVar(m)
  {
    //wall("isuvar " + m);
    var comps = m.split(".");
    getSlot(comps[0]).getSlot(comps[1]).isA(UVar)
  };
  /// Return a dot-formatted string with the data flow graph.
  function makeDot()
  {
    dotify(makeGraph)
  };
  /// Return the dataflow graph as [nodes, edges].
  function makeGraph()
  {
    var nodes = Dictionary.new;
    var edges = [];
    // check all uobjects, pass 1: create UVar nodes
    for| (var s: uobjects.localSlotNames)
    {
      var o = uobjects.getSlot(s);
      if (debug) wall("processing " + o);
      if (!o.isA(UObject) || !o.hasLocalSlot("__uobjectName"))
        continue;
      var oname = o.__uobjectName;
      if (nodes.has(oname))
        continue;
      var olabel = niceName(s);
      if (showBind || showOwnership)
        nodes[oname] = ["protoName" => oname, "kind" => "object",
        "label"=>olabel];
      // process uvarbindings
      if (showBind)
      for|(var v: uvarBindings.getWithDefault(oname, []))
      {
        var vname = v.asString.replace(".", "__");
        wall("checking " + oname +"<>" + vname.split("__")[0]);
        if (vname.split("__")[0] == oname)
          continue;
        edges.insertBack([oname, vname, ["len" => 2,  "style" => "dotted"]]);
      };
      // Check all its uvars
      for| (var vn: o.localSlotNames)
      {
        var v = o.getSlot(vn).acceptVoid;
        if (!v.isA(UVar))
          continue;
        if (v.change.dict.empty && v.access.dict.empty
            && !usedVars.has(v.asString))
          continue;
        var vname = v.asString.replace(".", "__");
        var vlabel = { if (showOwnership) vn else olabel + "." + vn};
        // One node for the UVar.
        nodes[vname] =
          ["shape" => "diamond",  "label" => vlabel, "kind" =>"var",
          "useCount"=>0];
        // One edge to its owner.
        if (showOwnership)
          edges.insertBack([oname, vname,
            ["len" => 0.5 , "arrowhead" => "dot", "kind" => "owns"]]);
      }
    };
    // check all uobjects, pass 2: create links
    for| (var s: uobjects.localSlotNames)
    {
      var o = uobjects.getSlot(s);
      if (debug) wall("processing " + o);
      if (!o.isA(UObject) || !o.hasLocalSlot("__uobjectName")
          || o.__uobjectName != s)
        continue;
      var oname = o.__uobjectName;
      var olabel = niceName(s);
      // Check all its uvars
      for| (var vn: o.localSlotNames)
      {
        var v = o.getSlot(vn).acceptVoid;
        if (!v.isA(UVar))
          continue;
        if (v.change.dict.empty && v.access.dict.empty
            && !usedVars.has(v.asString))
          continue;
        var vname = v.asString.replace(".", "__");
        var vlabel = { if (showOwnership) vn else olabel + "." + vn};
        // One edge with notifyChange
        var handleNotify = closure(p, kind, arrowkind)
        {
          if (!p.hasSlot("target"))
            continue;
          // Create a node for the notifychange method
          var targetname = p.target + "__" + vname;
          var mlabel =
            { if (showOwnership) kind
            else niceName(p.target + "." + kind)};
          nodes[targetname] =
            ["shape" => "box", "label" => mlabel, "kind" => "method"];
          // Edge the node to the owner
          if (showOwnership)
          edges.insertBack([p.target, targetname,
            ["len" => 0.5, "arrowhead" => "dot", "kind" =>"owns"]]);
          // Edge the node to the uvar
          edges.insertBack([vname, targetname,
                ["len" => 1, "arrowhead" => arrowkind, "kind" => kind]]);
        };
        for|(var c: v.change.dict)
        for|(var p: c.second.second)
        {
         handleNotify(p, "onChange", "empty");
        };
        for|(var c: v.access.dict)
        for|(var p: c.second.second)
        {
         handleNotify(p, "onAccess", "invempty");
        }
      };
      var handleSetGet = closure(e, arrowHead, kind)
      {
        var method = e.first;
        // Create a node for the method if none exists
        // The method can be a function name, or a UVar name, in which case
        // it means notify on the uvar
        var mname = { if (isUVar(method)) oname + "__" else ""}
          + method.replace(".", "__");
        var hasEdges = false;
        // e.second is the list of UVars this method writes to
        for| (var v: e.second)
        {
          if (v.ownerName == oname &&
              ( filter >=2 ||
                 (filter >=1 && ["init", "new"].has(method.split(".")[1]))))
            continue;
          var vname = v.asString.replace(".", "__");
          if (!nodes.has(vname))
            continue;
          nodes[vname]["useCount"]++;
          edges.insertBack([mname, vname,
              ["len" => 1, "arrowhead" =>arrowHead,  "kind" => kind]]);
          hasEdges = true;
        };
        // Only insert a node for the method if we kept an edge at least.
        if (hasEdges)
          if (!nodes.has(mname))
        {
          nodes[mname] =
            ["shape" => "box", "label" => niceName(method), "kind" => "method"];
          if (showOwnership)
          edges.insertBack([oname, mname,
                   ["len" => 0.5, "arrowhead" => "dot", "kind" => "owns"]]);
        };
      };
      for|(var e: uvarSetters.getWithDefault(oname, [], false))
        handleSetGet(e, "normal", "writes");
      for|(var e: uvarSetters.getWithDefault(oname, [], false))
        handleSetGet(e, "inv", "reads");
    };
    // Remove unwanted nodes.
    if (filterNode)
    for|(var n: nodes)
    {
      if (n.second["kind"] == "var" && !n.second["useCount"])
      {
        if (debug) wall("Filtering out "  + n.first);
        // Remove the edge between the uvar and its owner
        nodes.erase(n.first);
        edges = edges.filter(closure(x) { x[1] != n.first});
      }
    };
    // Output result
    [nodes, edges]
  };

  /// Make a 'short' version with one node per UObject from the full graph
  function shortGraph(graph)
  {
    var onodes = graph[0];
    var oedges = graph[1];
    var nodes  = Dictionary.new;
    var edges = List.new;
    var hashedEdges = Dictionary.new;
    for|(var e:oedges)
    {
      var o1 = e[0].split("__")[0];
      var o2 = e[1].split("__")[0];
      if (o1 == o2)
        continue;
      nodes[o1] = ["label" => niceName(o1)];
      nodes[o2] = ["label" => niceName(o2)];
      var ne;
      var hash = o1 +"+" + o2;
      if (hashedEdges.has(hash))
        ne = hashedEdges[hash]
      else
        ne = hashedEdges[hash] = [o1, o2,
          ["label" => "", "arrowhead" => "normal"]];
      if (!ne[2]["label"].empty)
        ne[2]["label"] += " , ";
      ne[2]["label"] += { switch(e[2]["kind"]) {
      case "owns":
      case "writes":
        "writes " + niceName(e[1].replace("__", "."))
      case "reads":
        "reads " + niceName(e[1].replace("__", "."))
      case "onChange":
        "on " + niceName(e[0].replace("__", ".")) + " change"
      }};
    };
    [nodes, hashedEdges.asList.map(function(v) { v.second})]
  };

  /// Add UObject statistics data to the graph. Modifies the graph inplace.
  function addStats(graph, stats=uobjects.getStats)
  {
    for|(var n: graph[0])
    {
      // Map node name to stats name
      var sname = n.first;
      // Sublte, we have two kind of methods: bound methods (a__b -> a.b), and
      // notifychange, (funcowner__varowner__varname -> itself)
      if (n.second["kind"] == "method" && sname.split("__").size == 2)
        sname = sname.replace("__", ".");
      if (debug) wall("scanning for " + n.second["kind"] + " " + sname);
      if (!stats.has(sname))
        continue;
      var stat = stats[sname];
      var statString
      = "Called %s times<br/>Avg time: %s us<br/>"
        "Max time: %s us <br/>Min time: %s us"
        % [stat[3], stat[0], stat[2], stat[1]];
      n.second["label"] = CustomString.new(n.second["label"]
                                           + "<br/><FONT POINT-SIZE=\"8\">"
                                           + statString
                                           + "</FONT>")
    };
    graph
  };

  function killEdges(edges, name)
  {
    edges.filter(function(x) { x[0]!= name && x[1] != name})
  };

  function getUVar(name)
  {
    var names = name.split(".");
    if (hasSlot(names[0]) && getSlot(names[0]).hasSlot(names[1]))
      getSlot(names[0]).getSlot(names[1])
    else
      nil
  };

  // Handle RTC components.
  function filterRTC(graph)
  {
    var nodes = graph[0];
    var edges = graph[1];
    // First kill internal methods
    for|( var n: nodes)
    {
      if (n.second["kind"] == "method")
      {
        var names = n.first.split("__");
        var ob = getSlot(names[0]);
        if (ob.hasLocalSlot("get_kind") // executioncontext method: internal
         || ["connect_uvar", "get_component_profile_", "connect",
            "create_output_port"].has(names[1])
            )
        {
          edges = killEdges(edges, n.first);
          nodes.erase(n.first);
        }
      }
    };

    // Now create connector_profiles and links, update/create UVars
    for|(var on: uobjects.localSlotNames)
    {
      var o = getSlot(on);
      if (o.hasSlot("get_component_profile_") && o.__uobjectName == on)
        for|(var pp: o.get_component_profile["port_profiles"])
      {
        var nodeName = on +"__" + pp["name"];
        if (!nodes.has(nodeName))
          nodes[nodeName] =
                 [ "kind"=>"uvar", "label" => niceName(on+"."+pp["name"]),
                 "shape" => ["invtriangle", "triangle"][pp["polarity"]]]
        else
          nodes[nodeName]["shape"] =["invtriangle", "triangle"][pp["polarity"]];
        // Remove internal notifyAccess
        var notifyNodeName = on + "__" + on + "__" + pp["name"];
        nodes.erase(notifyNodeName);
        edges = killEdges(edges, notifyNodeName);
        for|(var c: pp["connectors"])
        {
          var cn = "connector_" + c["uid"];
          var clabel = c["name"];
          if (!nodes.has(cn))
          { // Connector not registered yet. Create its node.
            nodes[cn] = ["shape" => "ellipse", "kind" => "connector",
            "label" => clabel];
            // And create the edges to each port member
            for| (var m: c["portnames"])
            {
              var origm = m;
              var polarity = 0;
              var v = getUVar(m);
              // The above should always work but just be sure.
              if (v) { // beware, use the node is using the uobject name
                // and not the component name
                 m = v.ownerName + "." + v.initialName;
                 polarity = v.polarity;
                 if (!nodes.has(m.replace(".", "__"))) // was filtered out
                   nodes[m.replace(".", "__")] =
                 [ "kind"=>"uvar", "label" => origm,
                 "shape" => ["invtriangle", "triangle"][polarity]];
              };
              var edge = [m.replace(".", "__"), cn];
              if (polarity)
                edge = edge.reverse;
              edge.insertBack(
                ["kind" => "connection", "arrowhead" => "normal"]);
              edges.insertBack(edge);
            }
          }
        }
      }
    };
    [nodes, edges]
  };

  /// Output a dot-format string from nodes and edges.
  function dotify(graph)
  {
    var nodes = graph[0];
    var edges = graph[1];
    var res = "graph {\n graph [overlap=\"scale\"];\n";
    for|(var n: nodes)
    {
      res += n.first + "[";
      var noprop = true;
      for| (var e: n.second)
      {
        if (["shape", "label"].has(e.first))
        {
          if (!noprop)
            res += ", ";
          noprop = false;
          res += e.first + "=" + e.second.asPrintable +" ";
        }
      };
      res += "];\n";
    };
    for| (var e: edges)
    {
      res += e[0] + " -- " + e[1] + " [";
      var noprop = true;
      if (e.size > 2)
        for|(var n: e[2])
      {
        if (["shape", "len", "arrowhead", "label"].has(n.first))
        {
          if (!noprop)
            res += ", ";
          noprop = false;
          res += n.first + " = " + n.second.asPrintable;
        }
      };
      res += "]" + ";\n";
    };
    res += "}\n";
    res
  };

  function step()
  {
    var ngraph = filterRTC(addStats(makeGraph));
    if (makeShortGraph)
      ngraph = shortGraph(ngraph)|
    if (ngraph.asString.size != graph.asString.size)
    {
      graph = ngraph;
      var d = dotify(graph);
      var f = File.create("/tmp/dataflow.dot");
      f.remove;
      OutputStream.new(f) << d;
      var p = Process.new("neato", ["-Tjpeg", "/tmp/dataflow.dot"]);
      p.runTo("/tmp/dataflow.jpg");
      waituntil(p.done);
      //sleep(300ms);
      var content = File.new("/tmp/dataflow.jpg").content;
      File.new("/tmp/dataflow.jpg").remove;
      content.keywords = "jpeg 100 100";
      val = content;
    }
  };

  function run(period)
  {
    startMonitor;
    var f = File.create("/tmp/dataflow.dot");
    every|(period)
      step
  };

  var monitor = Tag.new;
  var uvarBindings = Dictionary.new;
  var uvarSetters = Dictionary.new;
  var uvarGetters = Dictionary.new;
  var usedVars = Dictionary.new;

  UVar.new(this, "val");
  var graph;
};

/* Test sequence
var a = all.new(0);
var b = all.new(0);
var a.compactName = "a";
var b.compactName = "b";
a.setNotifyChangeByName(b.&c);
all.setNotifyChangeByName(a.&lastChangeVal);
b.c = 12;
all2.writeByName(b.&c, 15);
b.b = 12;
b.setNotifyAccess(1);
all.setNotifyChangeByName(b.&lastAccessVal);
b.b;
*/
/* RTC test sequence
var a = all.new(0);
var a.compactName = "a";
create_component(FileSink, "fsink");
fsink.set_output_file("/tmp/out.fsink");
var cp = fsink.create_connector_profile;
var cp2 = fsink.create_connector_profile;
fsink.connect("val", cp);
a.connect("a", cp);
create_component(FileSink, "fsink2");
fsink2.set_output_file("/tmp/out.fsink2");
fsink.connect("val", cp2);
fsink2.connect("val", cp2);
a.connect("b", cp);
a.connect("b", cp2);
all2.writeByName(a.&b, 15);
all2.writeByName(a.&a, 15);
*/
