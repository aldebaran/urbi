%% Copyright (C) 2010-2012, Gostai S.A.S.
%%
%% This software is provided "as is" without warranty of any kind,
%% either expressed or implied, including but not limited to the
%% implied warranties of fitness for a particular purpose.
%%
%% See the LICENSE file for more information.

\section{Process}

A Process is a separated task handled by the underneath operating
system.

\begin{windows}
  Process is not yet supported under Windows.
\end{windows}

\subsection{Prototypes}
\begin{itemize}
\item \refObject{Object}
\end{itemize}

\subsection{Example}

The following examples runs the \command{cat} program, a Unix standard
command that simply copies on its (standard) output its (standard)
input.

\begin{urbiscript}
var p = Process.new("cat", []);
[00000004] Process cat
\end{urbiscript}

\noindent
Just created, this process is not running yet.  Use \lstinline|run| to
launch it.

\begin{urbiscript}
p.status;
[00000005] not started

p.run;
p.status;
[00000006] running
\end{urbiscript}

\noindent
Then we feed its input, named \lstinline|stdin| in the Unix
tradition, and close its input.

\begin{urbiscript}
p.stdin << "1\n" |
p.stdin << "2\n" |
p.stdin << "3\n" |;

p.status;
[00000007] running

p.stdin.close;
\end{urbiscript}

\noindent
At this stage, the status of the process is unknown, as it is running
asynchronously.  If it has had enough time to ``see'' that its input
is closed, then it will have finished, otherwise we might have to wait
for awhile.  The method \lstinline|join| means ``wait for the process
to finish''.

\begin{urbiscript}
p.join;

p.status;
[00000008] exited with status 0
\end{urbiscript}

\noindent
Finally we can check its output.

\begin{urbiscript}
p.stdout.asList;
[00000009] ["1", "2", "3"]
\end{urbiscript}

\subsection{Construction}

A Process needs a program name to run and a possibly-empty list of
command line arguments.  Calling \lstinline|run| is required to
execute the process.

\begin{urbiscript}
Process.new("cat", []);
[00000004] Process cat

Process.new("cat", ["--version"]);
[00000004] Process cat
\end{urbiscript}

\subsection{Slots}

\begin{urbiscriptapi}
\item[asProcess] Return \this.
\begin{urbiscript}
do (Process.new("cat", []))
{
  assert (asProcess === this);
}|;
\end{urbiscript}

\item[asString] \lstinline|Process| and the name of the program.
\begin{urbiassert}
Process.new("cat", ["--version"]).asString
  == "Process cat";
\end{urbiassert}

\item[done] Whether the process has completed its execution.
\begin{urbiscript}
do (Process.new("sleep", ["1"]))
{
  assert (!done);
  run;
  assert (!done);
  join;
  assert (done);
}|;
\end{urbiscript}


\item[join] Wait for the process to finish.  Changes its status.
\begin{urbiscript}
do (Process.new("sleep", ["2"]))
{
  var t0 = System.time;
  assert (status.asString == "not started");
  run;
  assert (status.asString == "running");
  join;
  assert (t0 + 2s <= System.time);
  assert (status.asString == "exited with status 0");
}|;
\end{urbiscript}

\item[kill] If the process is not \lstinline|done|, interrupt it (with
  a \lstinline|SIGKILL| in Unix parlance).  You still have to wait for
  its termination with \lstinline|join|.
\begin{urbiscript}
do (Process.new("sleep", ["1"]))
{
  run;
  kill;
  join;
  assert (done);
  assert (status.asString == "killed by signal 9");
}|;
\end{urbiscript}


\item[name] The (base) name of the program the process runs.
\begin{urbiassert}
Process.new("cat", ["--version"]).name == "cat";
\end{urbiassert}

\item[run] Launch the process.  Changes it status.  A process can only
  be run once.
\begin{urbiscript}
do (Process.new("sleep", ["1"]))
{
  assert (status.asString == "not started");
  run;
  assert (status.asString == "running");
  join;
  assert (status.asString == "exited with status 0");
  run;
}|;
[00021972:error] !!! run: Process was already run
\end{urbiscript}

\item[runTo]
  %%% FIXME:
\item[status] An object whose slots describe the status of the
  process.
  %%% FIXME:
\item[stderr] An \refObject{InputStream} (the output of the Process is
  an input for \urbi) to the standard error stream of the process.
\begin{urbiscript}
do (Process.new("urbi-send", ["--no-such-option"]))
{
  run;
  join;
  assert
  {
    stderr.asList ==
    ["urbi-send: invalid option: --no-such-option",
     "Try `urbi-send --help' for more information."];
  };
}|;
\end{urbiscript}

\item[stdin] An \refObject{OutputStream} (the input of the Process is
  an output for \urbi) to the standard input stream of the process.
\begin{urbiscript}
do (Process.new(System.programName, ["--version"]))
{
  run;
  join;
  assert
  {
    stdout.asList[1] == "Copyright (C) 2004-2012 Gostai S.A.S..";
  };
}|;
\end{urbiscript}

\item[stdout] An \refObject{InputStream} (the output of the Process is
  an input for \urbi) to the standard output stream of the process.
\begin{urbiscript}
do (Process.new("cat", []))
{
  run;
  stdin << "Hello, World!\n";
  stdin.close;
  join;
  assert (stdout.asList == ["Hello, World!"]);
}|;
\end{urbiscript}
\end{urbiscriptapi}


%%% Local Variables:
%%% coding: utf-8
%%% mode: latex
%%% TeX-master: "../urbi-sdk"
%%% ispell-dictionary: "american"
%%% ispell-personal-dictionary: "../urbi.dict"
%%% fill-column: 76
%%% End:
