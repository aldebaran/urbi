%% Copyright (C) 2008-2010, Gostai S.A.S.
%%
%% This software is provided "as is" without warranty of any kind,
%% either expressed or implied, including but not limited to the
%% implied warranties of fitness for a particular purpose.
%%
%% See the LICENSE file for more information.

\section{List}

\lstinline|List|s implement possibly-empty ordered (heterogeneous)
collections of objects.

\subsection{Prototypes}

\begin{refObjects}
\item[Container]
\item[RangeIterable]
\item[Orderable]
\end{refObjects}

\subsection{Examples}

Since lists are \refObject{RangeIterable}, they also support
\refSlot[RangeIterable]{all} and \refSlot[RangeIterable]{any}.

\begin{urbiassert}
// Are all elements positive?
! [-2, 0, 2, 4].all(function (e) { 0 < e });
// Are all elements even?
  [-2, 0, 2, 4].all(function (e) { e % 2 == 0 });

// Is there any even element?
! [-3, 1, -1].any(function (e) { e % 2 == 0 });
// Is there any positive element?
  [-3, 1, -1].any(function (e) { 0 < e });
\end{urbiassert}


\subsection{Construction}

Lists can be created with their literal syntax: a possibly empty sequence of
expressions in square brackets, separated by commas.  Non-empty lists may
actually end with a comma.

\begin{urbiscript}
[]; // The empty list
[00000000] []
[1, "2", [3,],];
[00000000] [1, "2", [3]]
\end{urbiscript}

However, \lstinline|new| can be used as expected.

\begin{urbiscript}
List.new;
[00000001] []
[1, 2, 3].new;
[00000002] [1, 2, 3]
\end{urbiscript}

\subsection{Slots}

\begin{urbiscriptapi}
\item[append](<that>)%
  Deprecated alias for \refSlot{'+='}.

\begin{urbiscript}
var one = [1]|;
one.append(["one", [1]]);
[00000005:warning] !!! `list.append(that)' is deprecated, use `list += that'
[00000005] [1, "one", [1]]
\end{urbiscript}

\item \labelSlot{argMax}\lstinline|(\var{fun} = function(a, b) { a < b })|\\%
  The index of the (leftmost) ``largest'' member based on the comparison
  function \var{fun}.
\begin{urbiassert}
           [1].argMax == 0;
        [1, 2].argMax == 1;
     [1, 2, 2].argMax == 1;
        [2, 1].argMax == 0;
[2, -1, 3, -4].argMax == 2;

[2, -1, 3, -4].argMax (function (a, b) { a.abs < b.abs }) == 3;
\end{urbiassert}

The list cannot be empty.

\begin{urbiscript}
[].argMax;
[00000007:error] !!! argMax: list cannot be empty
\end{urbiscript}


\item \labelSlot{argMin}\lstinline|(\var{fun} = function(a, b) { a < b })|\\%
  The index of the (leftmost) ``smallest'' member based on the comparison
  function \var{fun}.
\begin{urbiassert}
           [1].argMin == 0;
        [1, 2].argMin == 0;
     [1, 2, 1].argMin == 0;
        [2, 1].argMin == 1;
[2, -1, 3, -4].argMin == 3;

[2, -1, 3, -4].argMin (function (a, b) { a.abs < b.abs }) == 1;
\end{urbiassert}

The list cannot be empty.

\begin{urbiscript}
[].argMin;
[00000011:error] !!! argMin: list cannot be empty
\end{urbiscript}

\item[asBool]
  Whether not empty.
\begin{urbiassert}
[].asBool == false;
[1].asBool == true;
\end{urbiassert}

\item[asList]
  Return the target.

\begin{urbiscript}
{
  var l = [0, 1, 2];
  assert (l.asList === l);
};
\end{urbiscript}

\item[asString]
  A string describing the list.  Uses \lstinline|asPrintable| on its
  members, so that, for instance, strings are displayed with quotes.

\begin{urbiassert}
[0, [1], "2"].asString == "[0, [1], \"2\"]";
\end{urbiassert}

\item[back]
  The last element of the target. An error if the target is empty.

\begin{urbiscript}
assert([0, 1, 2].back == 2);
[].back;
[00000017:error] !!! back: cannot be applied onto empty list
\end{urbiscript}

\item[clear]
  Empty the target.

\begin{urbiscript}
var x = [0, 1, 2];
[00000000] [0, 1, 2]
assert(x.clear == []);
\end{urbiscript}

\item[each](<fun>)%
  Apply the given functional value \var{fun} on all members,
  sequentially.

\begin{urbiscript}
[0, 1, 2].each(function (v) {echo (v * v); echo (v * v)});
[00000000] *** 0
[00000000] *** 0
[00000000] *** 1
[00000000] *** 1
[00000000] *** 4
[00000000] *** 4
\end{urbiscript}

\item[eachi](<fun>)%
  Apply the given functional value \var{fun} on all members
  sequentially, additionally passing the current element index.

\begin{urbiscript}
["a", "b", "c"].eachi(function (v, i) {echo ("%s: %s" % [i, v])});
[00000000] *** 0: a
[00000000] *** 1: b
[00000000] *** 2: c
\end{urbiscript}

\item['each&'](<fun>)%
Apply the given functional value on all members simultaneously.

\begin{urbiscript}
[0, 1, 2].'each&'(function (v) {echo (v * v); echo (v * v)});
[00000000] *** 0
[00000000] *** 1
[00000000] *** 4
[00000000] *** 0
[00000000] *** 1
[00000000] *** 4
\end{urbiscript}

\item[empty]
  Whether the target is empty.

\begin{urbiassert}
   [].empty;
! [1].empty;
\end{urbiassert}

\item[filter](<fun>)%
  The list of all the members of the target that verify the predicate
  \var{fun}.

\begin{urbiscript}
do ([0, 1, 2, 3, 4, 5])
{
  assert
  {
    // Keep only odd numbers.
    filter(function (v) {v % 2 == 1}) == [1, 3, 5];
    // Keep all.
    filter(function (v) { true })     == this;
    // Keep none.
    filter(function (v) { false })    == [];
  };
}|;
\end{urbiscript}

\item[foldl](<action>, <value>)%
  \wref[Fold_(higher-order_function)]{Fold},
  also known as \dfn{reduce} or \dfn{accumulate}, computes a result
  from a list.  Starting from \var{value} as the initial result, apply
  repeatedly the binary \var{action} to the current result and the
  next member of the list, from left to right.  For instance, if
  \var{action} were the binary addition and \var{value} were 0, then
  folding a list would compute the sum of the list, including for
  empty lists.

\begin{urbiassert}
       [].foldl(function (a, b) { a + b }, 0) == 0;
[1, 2, 3].foldl(function (a, b) { a + b }, 0) == 6;
[1, 2, 3].foldl(function (a, b) { a - b }, 0) == -6;
\end{urbiassert}

\item[front]
  Return the first element of the target. An error if the target is
  empty.
\begin{urbiscript}
assert([0, 1, 2].front == 0);
[].front;
[00000000:error] !!! front: cannot be applied onto empty list
\end{urbiscript}

\item[has](<that>)%
  Whether one of the members of the target equals the argument.

\begin{urbiassert}
[0, 1, 2].has(1);
! [0, 1, 2].has(5);
\end{urbiassert}

  The infix operators \lstinline|in| and \lstinline|not in| use
  \lstinline|has| (see \autoref{sec:lang:operators:containers}).

\begin{urbiassert}
  1 in     [0, 1];
  2 not in [0, 1];
!(2 in     [0, 1]);
!(1 not in [0, 1]);
\end{urbiassert}

\item[hash] Return a \refObject{Hash} object corresponding to this list
  value. Two list hashes are equal if the list have the same size, and
  elements hashes are equal two by two. See \refSlot[Object]{hash}.

\begin{urbiassert}
[].hash.isA(Hash);
[].hash == [].hash;
[1, "foo"].hash == [1, "foo"].hash;
[0, 1].hash != [1, 0].hash;
\end{urbiassert}

\item[hasSame](<that>)%
  Whether one of the members of the target is physically equal to
  \var{that}.
\begin{urbiscript}
var y = 1|;
assert
{
   [0, y, 2].hasSame(y);
  ![0, y, 2].hasSame(1);
};
\end{urbiscript}

\item[head]
  Synonym for \refSlot{front}.
\begin{urbiscript}
assert([0, 1, 2].head == 0);
[].head;
[00000000:error] !!! head: cannot be applied onto empty list
\end{urbiscript}

\item[insert](<where>, <what>)%
  Insert \var{what} before the value at index \var{where}, return
  \this.
\begin{urbiscript}
{
  var l = [0, 1];
  assert
  {
    l.insert(0, 10) === l;
    l == [10, 0, 1];
    l.insert(2, 20) === l;
    l == [10, 0, 20, 1];
  };
}|;
\end{urbiscript}

  The index must be valid, to insert past the end, use \refSlot{insertBack}.
\begin{urbiscript}
[].insert(0, "foo");
[00044239:error] !!! insert: invalid index: 0
[1, 2, 3].insert(4, 30);
[00044339:error] !!! insert: invalid index: 4
\end{urbiscript}


\item[insertBack](<that>)%
  Insert the given element at the end of the target, return \this.

\begin{urbiscript}
{
  var l = [0, 1];
  assert
  {
    l.insertBack(2) === l;
    l == [0, 1, 2];
  };
}|;
\end{urbiscript}

\item[insertFront](<that>)%
  Insert the given element at the beginning of the target.  Return \this.

\begin{urbiscript}
{
  var l = [0, 1];
  assert
  {
    l.insertFront(0) === l;
    l == [0, 0, 1];
  };
}|;
\end{urbiscript}

\item[insertUnique](<that>)%
  If \var{that} is not in \this, append it. Return \this.

\begin{urbiscript}
{
  var l = [0, 1];
  assert
  {
    l.insertUnique(0) === l;
    l == [0, 1];
    l.insertUnique(2) === l;
    l == [0, 1, 2];
  };
};
\end{urbiscript}

\item[join](<sep> = "", <prefix> = "", <suffix> = "")%
  Bounce to \refSlot[String]{join}.

\begin{urbiassert}
["", "ob", ""].join                == "ob";
["", "ob", ""].join("a")           == "aoba";
["", "ob", ""].join("a", "B", "b") == "Baobab";
\end{urbiassert}

\item[keys]()%
  The list of valid indexes.  This allows uniform iteration over a
  \refObject{Dictionary} or a \refObject{List}.

\begin{urbiscript}
{
  var l = ["a", "b", "c"];
  assert
  {
    l.keys == [0, 1, 2];
    {
      var res = [];
      for (var k: l.keys)
        res << l[k];
      res
    }
    == l;
  };
};
\end{urbiscript}

\item[map](<fun>)%
  Apply the given functional value on every member, and return the list of
  results.

\begin{urbiassert}
[0, 1, 2, 3].map(function (v) { v % 2 == 0})
        == [true, false, true, false];
\end{urbiassert}

\item[matchAgainst](<handler>, <pattern>)%
  If \var{pattern} is a List of same size, use \var{handler} to match each
  member of \this against the corresponding \var{pattern}.  Return true if
  the match succeeded, false in other cases.
\begin{urbiscript}
assert
{
  ([1, 2] = [1, 2]) == [1, 2];

  ([1, var a] = [1, 2]) == [1, 2];
  a == 2;

  ([var u, var v, var w] = [1, 2, 3]) == [1, 2, 3];
  [u, v, w] == [1, 2, 3];
};

[1, 2] = [2, 1];
[00005863:error] !!! pattern did not match

[1, var a] = [2, 1];
[00005864:error] !!! pattern did not match
[1, var a] = [1];
[00005865:error] !!! pattern did not match
[1, var a] = [1, 2, 3];
[00005865:error] !!! pattern did not match
\end{urbiscript}

\item \labelSlot{max}\lstinline|(\var{fun} = function(a, b) { a < b })|\\%
  Return the ``largest'' member based on the comparison function \var{fun}.
\begin{urbiassert}
           [1].max == 1;
        [1, 2].max == 2;
        [2, 1].max == 2;
[2, -1, 3, -4].max == 3;

[2, -1, 3, -4].max (function (a, b) { a.abs < b.abs }) == -4;
\end{urbiassert}

The list cannot be empty.

\begin{urbiscript}
[].max;
[00000001:error] !!! max: list cannot be empty
\end{urbiscript}

The members must be comparable.
\begin{urbiscript}
[0, 2, "a", 1].max;
[00000002:error] !!! max: argument 2: unexpected "a", expected a Float
\end{urbiscript}

\item \labelSlot{min}\lstinline|(\var{fun} = function(a, b) { a < b })|\\%
  Return the ``smallest'' member based on the comparison function \var{fun}.
\begin{urbiassert}
           [1].min == 1;
        [1, 2].min == 1;
        [2, 1].min == 1;
[2, -1, 3, -4].min == -4;

[2, -1, 3, -4].min (function (a, b) { a.abs < b.abs }) == -1;
\end{urbiassert}

The list cannot be empty.

\begin{urbiscript}
[].min;
[00000001:error] !!! min: list cannot be empty
\end{urbiscript}

\item[range](<begin>, <end> = nil)%
  Return a sub-range of the list, from the first index included to the
  second index excluded.  An error if out of bounds.  Negative indices
  are valid, and number from the end.

  If \var{end} is \lstinline|nil|, calling \lstinline|range(\var{n})|
  is equivalent to calling \lstinline|range(0, \var{n})|.

\begin{urbiscript}
do ([0, 1, 2, 3])
{
  assert
  {
    range(0, 0)   == [];
    range(0, 1)   == [0];
    range(1)      == [0];
    range(1, 3)   == [1, 2];

    range(-3, -2) == [1];
    range(-3, -1) == [1, 2];
    range(-3, 0)  == [1, 2, 3];
    range(-3, 1)  == [1, 2, 3, 0];
    range(-4, 4)  == [0, 1, 2, 3, 0, 1, 2, 3];
  };
}|;
[].range(1, 3);
[00428697:error] !!! range: invalid index: 1
\end{urbiscript}

\item[remove](<val>)%
  Remove all elements from the target that are equal to \var{val}, return
  \this.

\begin{urbiscript}
var c = [0, 1, 0, 2, 0, 3]|;
assert
{
  c.remove(0) === c;   c ==  [1, 2, 3];
  c.remove(42) === c;  c ==  [1, 2, 3];
};
\end{urbiscript}

\item[removeBack]
  Remove and return the last element of the target. An error if the
  target is empty.

\begin{urbiscript}
var t = [0, 1, 2];
[00000000] [0, 1, 2]
assert(t.removeBack == 2);
assert(t == [0, 1]);
[].removeBack;
[00000000:error] !!! removeBack: cannot be applied onto empty list
\end{urbiscript}

\item[removeById](<that>)%
  Remove all elements from the target that physically equals
  \var{that}.

\begin{urbiscript}
var d = 1|;
var e = [0, 1, d, 1, 2]|;
assert
{
  e.removeById(d) == [0, 1, 1, 2];
  e == [0, 1, 1, 2];
};
\end{urbiscript}

\item[removeFront]
Remove and return the first element from the target. An error if the
target is empty.

\begin{urbiscript}
var g = [0, 1, 2]|;
assert
{
  g.removeFront == 0;
  g == [1, 2];
};
[].removeFront;
[00000000:error] !!! removeFront: cannot be applied onto empty list
\end{urbiscript}

\item[reverse]
Return the target with the order of elements inverted.

\begin{urbiassert}
[0, 1, 2].reverse == [2, 1, 0];
\end{urbiassert}

\item[size]
Return the number of elements in the target.

\begin{urbiassert}
[0, 1, 2].size == 3;
[].size == 0;
\end{urbiassert}

\item \labelSlot{sort}\lstinline|(\var{fun} = function(a, b) { a < b })|\\%
  A new List with the contents of \this, sorted with respect to the
  \var{comp} comparison function.

\begin{urbiscript}
{
  var l = [3, 0, -2, 1];
  assert
  {
    l.sort == [-2, 0, 1, 3];
    l      == [3, 0, -2, 1];

    l.sort(function(a, b) {a.abs < b.abs})
           == [0, 1, -2, 3];
  };
};
\end{urbiscript}

\item[subset](<that>)%
  Whether the members of \this are members of \var{that}.

\begin{urbiassert}
        [].subset([]);
        [].subset([1, 2, 3]);
 [3, 2, 1].subset([1, 2, 3]);
    [1, 3].subset([1, 2, 3]);
    [1, 1].subset([1, 2, 3]);

      ![3].subset([]);
   ![3, 2].subset([1, 2]);
![1, 2, 3].subset([1, 2]);
\end{urbiassert}

\item[tail]
  Return the target, minus the first element. An error if the target
  is empty.

\begin{urbiscript}
assert([0, 1, 2].tail == [1, 2]);
[].tail;
[00000000:error] !!! tail: cannot be applied onto empty list
\end{urbiscript}

\item[zip](<fun>, <other>)%
  Zip \this list and the \var{other} list with the \var{fun} function, and
  return the list of results.

\begin{urbiassert}
[1, 2, 3].zip(closure (x, y) { (x, y) }, [4, 5, 6])
       == [(1, 4), (2, 5), (3, 6)];
[1, 2, 3].zip(closure (x, y) { x + y }, [4, 5, 6])
       == [5, 7, 9];
\end{urbiassert}

\item['=='](<that>)%
Check whether all elements in the target and \var{that}, are
equal two by two.

\begin{urbiassert}
[0, 1, 2] == [0, 1, 2];
!([0, 1, 2] == [0, 0, 2]);
\end{urbiassert}

\item|'[]'|(<n>)%
  Return the \var{n}th member of the target (indexing is
  zero-based). If \var{n} is negative, start from the end.  An error
  if out of bounds.

\begin{urbiscript}
assert
{
  ["0", "1", "2"][0] == "0";
  ["0", "1", "2"][2] == "2";
};
["0", "1", "2"][3];
[00007061:error] !!! []: invalid index: 3

assert
{
  ["0", "1", "2"][-1] == "2";
  ["0", "1", "2"][-3] == "0";
};
["0", "1", "2"][-4];
[00007061:error] !!! []: invalid index: -4
\end{urbiscript}

\item|'[]='|(<index>, <value>)%
  Assign \var{value} to the element of the target at the given
  \var{index}.

\begin{urbiscript}
var f = [0, 1, 2];
[00000000] [0, 1, 2]
assert
{
  (f[1] = 42) == 42;
  f == [0, 42, 2];
};

for (var i: [0, 1, 2])
  f[i] = 10 * f[i];
assert (f == [0, 420, 20]);
\end{urbiscript}

\item['*'](<n>)%
  Return the target, concatenated \var{n} times to itself.
\begin{urbiassert}
[0, 1] * 0 == [];
[0, 1] * 3 == [0, 1, 0, 1, 0, 1];
\end{urbiassert}

  \var{n} must be a non-negative integer.

\begin{urbiscript}
[0, 1] * -2;
[00000063:error] !!! *: argument 1: expected non-negative integer, got -2
\end{urbiscript}


  Note that since it is the very same list which is repeatedly
  concatenated (the content is not cloned), side-effects on one item
  will reflect on ``all the items''.

\begin{urbiscript}
var l = [[]] * 3;
[00000000] [[], [], []]
l[0] << 1;
[00000000] [1]
l;
[00000000] [[1], [1], [1]]
\end{urbiscript}

\item['+'](<other>)%
  Return the concatenation of the target and the \var{other} list.

\begin{urbiassert}
[0, 1] + [2, 3] == [0, 1, 2, 3];
    [] + [2, 3] == [2, 3];
[0, 1] + []     == [0, 1];
    [] + []     == [];
\end{urbiassert}

The target is left unmodified (contrary to \refSlot{'+='}).
\begin{urbiscript}
{
  var l = [1, 2, 3];
  assert
  {
    l + l == [1, 2, 3, 1, 2, 3];
    l     == [1, 2, 3];
  };
};
\end{urbiscript}

\item['+='](<that>)%
  Concatenate the contents of the List \var{that} to \this, and return
  \this.  This function modifies its target, contrary to \refSlot{'+'}.  See
  also \refSlot{'<<'}.

\begin{urbiscript}
{
  var l = [];
  var alias = l;
  assert
  {
    (l += [1, 2]) == l;
    l == [1, 2];
    (l += [3, 4]) == l;
    l == [1, 2, 3, 4];
    alias == [1, 2, 3, 4];
  };
};
\end{urbiscript}

\item['-'](<other>)%
  Return the target without the elements that are equal to any element
  in the \var{other} list.

\begin{urbiassert}
[0, 1, 0, 2, 3] - [1, 2] == [0, 0, 3];
[0, 1, 0, 1, 0] - [1, 2] == [0, 0, 0];
\end{urbiassert}

\item['<<'](<that>)%
  A synonym for \lstinline|insertBack|.

\item['<'](<other>)%
  Return whether \this is less than the \var{other} list.  This is the
  lexicographic comparison: \this is ``less that'' if one of its member is
  ``less than'' the corresponding member of \var{other}:

\begin{urbiassert}
  [0, 0, 0] < [0, 0, 1];
  [0, 1, 2] < [0, 2, 1];

!([0, 1, 2] < [0, 1, 2]);
!([0, 1, 2] < [0, 0, 2]);
\end{urbiassert}

  \noindent
  or \var{other} is a prefix (strict) of \this:

\begin{urbiassert}
           [] < [0];          !(      [0] < []);
       [0, 1] < [0, 1, 2];    !([0, 1, 2] < [0, 1]);
  !([0, 1, 2] < [0, 1, 2]);
\end{urbiassert}

  Since List derives from \refObject{Orderable}, the other order-based
  operators are defined.

\begin{urbiassert}
        [] <= [];
        [] <= [0, 1, 2];
 [0, 1, 2] <= [0, 1, 2];

        [] >= [];
 [0, 1, 2] >= [];
 [0, 1, 2] >= [0, 1, 2];
 [0, 1, 2] >= [0, 0, 2];

       !([] > []);
  [0, 1, 2] > [];
!([0, 1, 2] > [0, 1, 2]);
  [0, 1, 2] > [0, 0, 2];
\end{urbiassert}
\end{urbiscriptapi}

%%% Local Variables:
%%% coding: utf-8
%%% mode: latex
%%% TeX-master: "../urbi-sdk"
%%% ispell-dictionary: "american"
%%% ispell-personal-dictionary: "../urbi.dict"
%%% fill-column: 76
%%% End:
