%% Copyright (C) 2009-2011, Gostai S.A.S.
%%
%% This software is provided "as is" without warranty of any kind,
%% either expressed or implied, including but not limited to the
%% implied warranties of fitness for a particular purpose.
%%
%% See the LICENSE file for more information.

\section{Tag}

A \dfn{tag} is an object meant to label blocks of code in order to control
them externally.  Tagged code can be frozen, resumed, stopped\ldots See also
\autoref{sec:tut:tags}.

\subsection{Examples}

\subsubsection{Stop}
\label{sec:specs:tag:stop}

To \dfn{stop} a tag means to kill all the code currently running that it
labels.  It does not affect ``newcomers''.

\begin{urbiscript}[firstnumber=1]
var t = Tag.new|;
var t0 = time|;
t: every(1s) echo("foo"),
sleep(2.2s);
[00000158] *** foo
[00001159] *** foo
[00002159] *** foo

t.stop;
// Nothing runs.
sleep(2.2s);

t: every(1s) echo("bar"),
sleep(2.2s);
[00000158] *** bar
[00001159] *** bar
[00002159] *** bar

t.stop;
\end{urbiscript}

\refSlot[Tag]{stop} can be used to inject a return value to a tagged
expression.

\begin{urbiscript}[firstnumber=1]
var t = Tag.new|;
var res;
detach(res = { t: every(1s) echo("computing") })|;
sleep(2.2s);
[00000001] *** computing
[00000002] *** computing
[00000003] *** computing

t.stop("result");
assert(res == "result");
\end{urbiscript}

Be extremely cautious, the precedence rules can be misleading:
\lstinline|\var{var} = \var{tag}: \var{exp}| is read as
\lstinline|(\var{var} = \var{tag}): \var{exp}| (i.e., defining \var{var} as
an alias to \var{tag} and using it to tag \var{exp}), not as
\lstinline|\var{var} = { \var{tag}: \var{exp} }|.  Contrast the following
example, which is most probably an error from the user, with the previous,
correct, one.

\begin{urbiscript}[firstnumber=1]
var t = Tag.new("t")|;
var res;
res = t: every(1s) echo("computing"),
sleep(2.2s);
[00000001] *** computing
[00000002] *** computing
[00000003] *** computing

t.stop("result");
assert(res == "result");
[00000004:error] !!! failed assertion: res == "result" (Tag<t> != "result")
\end{urbiscript}


\subsubsection{Block/unblock}
\label{sec:specs:tag:block}

To \dfn{block} a tag means:
\begin{itemize}
\item Stop running pieces of code it labels (as with
  \refSlot{stop}).
\item Ignore new pieces of code it labels (this differs from
  \refSlot{stop}).
\end{itemize}

One can \dfn{unblock} the tag.  Contrary to
\refSlot{freeze}/\refSlot{unfreeze}, tagged code does not resume the
execution.

\begin{urbiscript}[firstnumber=1]
var ping = Tag.new("ping")|;
ping:
  every (1s)
    echo("ping"),
assert(!ping.blocked);
sleep(2.1s);
[00000000] *** ping
[00002000] *** ping
[00002000] *** ping

ping.block;
assert(ping.blocked);

ping:
  every (1s)
    echo("pong"),

// Neither new nor old code runs.
ping.unblock;
assert(!ping.blocked);
sleep(2.1s);

// But we can use the tag again.
ping:
  every (1s)
    echo("ping again"),
sleep(2.1s);
[00004000] *** ping again
[00005000] *** ping again
[00006000] *** ping again
\end{urbiscript}

As with \refSlot{stop}, one can force the value of stopped
expressions.

\begin{urbiassert}[firstnumber=1]
{
  var t = Tag.new;
  var res = [];
  for (3)
    detach(res << {t: sleep});
  t.block("foo");
  res;
}
==
["foo", "foo", "foo"];
\end{urbiassert}

\subsubsection{Freeze/unfreeze}
\label{sec:specs:tag:freeze}

To \dfn{freeze} a tag means holding the execution of code it labels.
This applies to code already being run, and ``arriving'' pieces of code.

\begin{urbiscript}[firstnumber=1]
var t = Tag.new|;
var t0 = time|;
t: every(1s) echo("time   : %.0f" % (time - t0)),
sleep(2.2s);
[00000158] *** time   : 0
[00001159] *** time   : 1
[00002159] *** time   : 2

t.freeze;
assert(t.frozen);
t: every(1s) echo("shifted: %.0f" % (shiftedTime - t0)),
sleep(2.2s);
// The tag is frozen, nothing is run.

// Unfreeze the tag: suspended code is resumed.
// Note the difference between "time" and "shiftedTime".
t.unfreeze;
assert(!t.frozen);
sleep(2.2s);
[00004559] *** shifted: 2
[00005361] *** time   : 5
[00005560] *** shifted: 3
[00006362] *** time   : 6
[00006562] *** shifted: 4
\end{urbiscript}


\subsubsection{Scope tags}
\label{sec:specs:tag:scope}

Scopes feature a \lstindex{scopeTag}, i.e., a tag which will be stopped when
the execution reaches the end of the current scope.  This is handy to
implement cleanups, however the scope was exited from.

\begin{urbiscript}[firstnumber=1]
{
  var t = scopeTag;
  t: every(1s)
      echo("foo"),
  sleep(2.2s);
};
[00006562] *** foo
[00006562] *** foo
[00006562] *** foo

{
  var t = scopeTag;
  t: every(1s)
      echo("bar"),
  sleep(2.2s);
  throw 42;
};
[00006562] *** bar
[00006562] *** bar
[00006562] *** bar
[00006562:error] !!! 42
sleep(2s);
\end{urbiscript}

\subsubsection{Enter/leave events}
\label{sec:specs:tag:enter-leave}

Tags provide two events, \refSlot{enter} and \refSlot{leave}, that trigger
whenever flow control enters or leaves tagged statements.

\begin{urbiscript}[firstnumber=1]
var t = Tag.new("t");
[00000000] Tag<t>

at (t.enter?)
  echo("enter");
at (t.leave?)
  echo("leave");

t: {echo("inside"); 42};
[00000000] *** enter
[00000000] *** inside
[00000000] *** leave
[00000000] 42
\end{urbiscript}

This feature is fundamental; it is a concise and safe way to ensure code
will be executed upon exiting a chunk of code (like \acro{raii} in \Cxx or
\lstinline|finally| in Java). The exit code will be run no matter what the
reason for leaving the block was: natural exit, exceptions, flow control
statements like \lstinline|return| or \lstinline|break|, \ldots

For instance, suppose we want to make sure we turn the gas off when
we're done cooking. Here is the \emph{bad} way to do it:

\begin{urbicomment}
function enterTheKitchen() {}|
function leaveTheKitchen() {}|
function turnGasOn() {}|
function turnGasOff() {}|

var mealReady = false|
var recipe = []|
var ingredient = 0|;
\end{urbicomment}
\begin{urbiscript}
{
  function cook()
  {
    turnGasOn();
    // Cooking code ...
    turnGasOff();
  }|

  enterTheKitchen();
  cook();
  leaveTheKitchen();
};
\end{urbiscript}

This \lstinline|cook| function is wrong because there are several situations
where we could leave the kitchen with gas still turned on. Consider the
following cooking code:

\begin{urbiscript}
{
  function cook()
  {
    turnGasOn();

    if (mealReady)
    {
      echo("The meal is already there, nothing to do!");
      // Oops ...
      return;
    };

    for (var i in recipe)
      if (i in kitchen)
        putIngredient(i)
      else
        // Oops ...
        throw Exception("missing ingredient: %s" % i);

    // ...

    turnGasOff();
  }|
};
\end{urbiscript}

Here, if the meal was already prepared, or if an ingredient is missing, we
will leave the \lstinline|cook| function without executing the
\lstinline|turnGasOff| statement, through the \lstinline|return| statement
or the exception.  One correct way to ensure gas is necessarily turned off
is:

\begin{urbiscript}
{
  function cook()
  {
    var withGas = Tag.new("withGas");

    at (withGas.enter?)
      turnGasOn();
    // Even if exceptions are thrown or return is called,
    // the gas will be turned off.
    at (withGas.leave?)
      turnGasOff();

    withGas: {
      // Cooking code...
    }
  }|
};
\end{urbiscript}

Alternatively, the \lstinline|try|/\lstinline|finally| construct provides an
elegant means to achieve the same result (\autoref{sec:lang:except:finally}).

\begin{urbiscript}
{
  function cook()
  {
    try
    {
      turnGasOn();
      // Cooking code...
    }
    finally
    {
      // Even if exceptions are thrown or return is called,
      // the gas will be turned off.
      turnGasOff();
    }
  }|
};
\end{urbiscript}

\subsubsection{Begin/end}
\label{sec:specs:tag:begin-end}

The \refSlot{begin} and \refSlot{end} methods enable to monitor when code is
executed.  The following example illustrates the proper use of
\refSlot{enter} and \refSlot{leave} events
(\autoref{sec:specs:tag:enter-leave}), which are used to implement this
feature.

\begin{urbiscript}
var myTag = Tag.new("myTag");
[00000000] Tag<myTag>

myTag.begin: echo(1);
[00000000] *** myTag: begin
[00000000] *** 1

myTag.end: echo(2);
[00000000] *** 2
[00000000] *** myTag: end

myTag.begin.end: echo(3);
[00000000] *** myTag: begin
[00000000] *** 3
[00000000] *** myTag: end
\end{urbiscript}

\subsection{Construction}
\label{stdlib:tag:ctor}

As any object, tags are created using \lstinline{new} to create derivatives
of the \lstinline{Tag} object.  The name is optional, it makes easier to
display a tag and remember what it is.

\begin{urbiscript}[firstnumber=1]
// Anonymous tag.
var t1 = Tag.new;
[00000001] Tag<tag_8>

// Named tag.
var t2 = Tag.new("cool name");
[00000001] Tag<cool name>
\end{urbiscript}

\subsection{Slots}

\begin{urbiscriptapi}
\item[begin]
  A sub-tag that prints out "tag\_name: begin" each time flow control
  enters the tagged code. See \autoref{sec:specs:tag:begin-end}.

\item[block](<result> = void)%
  Block any code tagged by \this.  Blocked tags can be
  unblocked using \refSlot{unblock}.  If some \var{result} was
  specified, let stopped code return \var{result} as value.  See
  \autoref{sec:specs:tag:block}.

\item[blocked]
  Whether code tagged by \this is blocked.  See
  \autoref{sec:specs:tag:block}.

\item[end]
  A sub-tag that prints out "tag\_name: end" each time flow control
  leaves the tagged code. See \autoref{sec:specs:tag:begin-end}.

\item[enter] An event triggered each time the flow control enters the
  tagged code.  See \autoref{sec:specs:tag:enter-leave}.

\item[freeze] Suspend code tagged by \this, already running or
  forthcoming.  Frozen code can be later unfrozen using \refSlot{unfreeze}.
  See \autoref{sec:specs:tag:freeze}.

\item[frozen]
  Whether the tag is frozen. See  \autoref{sec:specs:tag:freeze}.

\item[leave] An event triggered each time flow control leaves the
  tagged code.  See \autoref{sec:specs:tag:enter-leave}.

\item[scope] Return a fresh Tag whose \refSlot{stop} will be invoked a the
  end of the current scope.  This function is likely to be removed.  See
  \autoref{sec:specs:tag:scope}.

\item[stop](<result> = void)%
  Stop any code tagged by \this.  If some \var{result} was
  specified, let stopped code return \var{result} as value.
  See \autoref{sec:specs:tag:stop}.

\item[tags] All the undeclared tags are created as slots in this
  object.  Using this feature is discouraged.
\begin{urbiscript}
{
  assert ("brandNewTag" not in Tag.tags.localSlotNames);
  brandNewTag: 1;
  assert ("brandNewTag" in Tag.tags.localSlotNames);
  assert (Tag.tags.brandNewTag.isA(Tag));
};
\end{urbiscript}

\item[unblock]
  Unblock \this.  See \autoref{sec:specs:tag:block}.

\item[unfreeze]
  Unfreeze code tagged by \this.  See
  \autoref{sec:specs:tag:freeze}.
\end{urbiscriptapi}

\subsection{Hierarchical tags}

Tags can be arranged in a parent/child relationship: any operation done on a
tag --- freezing, stopping, \ldots is also performed on its descendants.
Another way to see it is that tagging a piece of code with a child will also
tag it with the parent. To create a child Tag, simply clone its parent.

\begin{urbiscript}
var parent = Tag.new |
var child = parent.clone |

// Stopping parent also stops children.
{
  parent: {sleep(100ms); echo("parent")},
  child:  {sleep(100ms); echo("child")},
  parent.stop;
  sleep(200ms);
  echo("end");
};
[00000001] *** end

// Stopping child has no effect on parent.
{
  parent: {sleep(100ms); echo("parent")},
  child:  {sleep(100ms); echo("child")},
  child.stop;
  sleep(200ms);
  echo("end");
};
[00000002] *** parent
[00000003] *** end
\end{urbiscript}

Hierarchical tags are commonly laid out in slots so as to reflect their tag
hierarchy.

\begin{urbiunchecked}
var a = Tag.new;
var a.b = a.clone;
var a.b.c = a.b.clone;

a:     foo; // Tagged by a
a.b:   bar; // Tagged by a and b
a.b.c: baz; // Tagged by a, b and c
\end{urbiunchecked}

% FIXME: If we ever restore some sugar to create hierarchical tags, document
% it here

%%% Local Variables:
%%% coding: utf-8
%%% mode: latex
%%% TeX-master: "../urbi-sdk"
%%% ispell-dictionary: "american"
%%% ispell-personal-dictionary: "../urbi.dict"
%%% fill-column: 76
%%% End:
